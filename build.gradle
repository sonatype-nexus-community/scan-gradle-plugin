plugins {
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'signing'
  id 'net.researchgate.release' version '2.6.0'
  id 'io.codearte.nexus-staging' version '0.21.2'
}

apply plugin: 'maven'

gradlePlugin {
  plugins {
    scan {
      id = 'org.sonatype.gradle.plugins.scan'
      implementationClass = 'org.sonatype.gradle.plugins.scan.ScanPlugin'
    }
  }
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
  manifest {
    attributes(
      'Build-Timestamp' : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
      'Build-Revision'  : project.version,
      'Built-By'        : 'Sonatype',
      'Created-By'      : "Gradle ${gradle.gradleVersion}",
      'Build-Jdk'       : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
    )
  }
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier "javadoc"
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourceJar
  archives javadocJar
}

repositories {
  mavenCentral()
}

dependencies {
  localGroovy()
  gradleApi()

  implementation "com.sonatype.nexus:nexus-platform-api:$nexusJavaApiVersion:internal"

  testCompile gradleTestKit()
  testCompile "junit:junit:$junitVersion"
  testCompile "org.assertj:assertj-core:$assertJVersion"
  testCompile "commons-io:commons-io:$commonsIoVersion"
  testCompile "org.powermock:powermock-module-junit4:$powermockVersion"
  testCompile "org.powermock:powermock-api-mockito2:$powermockVersion"
  
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integTest/java')
    resources.srcDir file('src/integTest/resources')
    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    runtimeClasspath += output + compileClasspath
  }
}

processResources {
  filesMatching('com/sonatype/insight/client.properties') {
      expand(project.properties)
  }
}

test {
  maxHeapSize = '512m'
}

task integrationTest(type: Test) {
  description = 'Runs the integration tests.'
  group = 'verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  maxHeapSize = '1536m'
  mustRunAfter test
}

check.dependsOn integrationTest

// configure deployment if performing release
if ('do-release'.equals(System.getenv('CIRCLE_JOB'))) {
  publishing {
    afterEvaluate {
      release {
        // avoid infinite CircleCI builds triggered by release commits
        preCommitText = '[skip ci] '
      }

      def sonatypeUsername = System.getenv('SONATYPE_USERNAME')
      def sonatypePassword = System.getenv('SONATYPE_PASSWORD')

      // for local testing
      //def serverUrlBase = 'http://192.168.1.233:8081/nexus/service/local/'
      def serverUrlBase = 'https://repository.sonatype.org:443/service/local/'

      repositories {
        maven {
          name = 'rso'
          url = serverUrlBase + 'staging/deploy/maven2/'
          credentials {
            username = sonatypeUsername
            password = sonatypePassword
          }
        }
      }

      nexusStaging {
        serverUrl = serverUrlBase
        // for local testing
        //stagingProfileId = 'd2d10f6cdd9'
        stagingProfileId = '97306992c3c7ca'
        username = sonatypeUsername
        password = sonatypePassword
      }

      publications {
        withType(MavenPublication) {
          pom {
            name = project.name
            description = 'Scan, evaluate and audit Gradle projects using Sonatype platforms'
            url = 'https://github.com/sonatype-nexus-community/scan-gradle-plugin'
            organization {
              name = 'Sonatype'
              url = 'https://www.sonatype.org'
            }
            issueManagement {
              system = 'GitHub'
              url = 'https://github.com/sonatype-nexus-community/scan-gradle-plugin/issues'
            }
            licenses {
              license {
                name = 'The Apache Software License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'repo'
              }
            }
            scm {
              connection = 'scm:git:git://github.com/sonatype-nexus-community/scan-gradle-plugin.git'
              developerConnection = 'scm:git:ssh://git@github.com:sonatype-nexus-community/scan-gradle-plugin.git'
              url = 'https://github.com/sonatype-nexus-community/scan-gradle-plugin'
            }
            developers {
              developer {
                name = 'Guillermo Varela'
              }
              developer {
                name = 'Usman Shaikh'
              }
            }
          }
  
          artifactId = project.name
  
          artifact(sourceJar) {
            classifier = 'sources'
          }
  
          artifact(javadocJar) {
            classifier = 'javadoc'
          }
        }
      }

      signing {
        def final encodedKey = System.getenv('SECRING_GPG_ASC_BASE64')
        def final signingKey = new String(encodedKey.decodeBase64())
        allprojects {
          println 'setup gpg signing with in-memory key'
          useInMemoryPgpKeys(
              signingKey,
              System.getenv('GPG_PASSPHRASE')
          )
        }

        sign publishing.publications.pluginMaven
        sign publishing.publications.scanPluginMarkerMaven
      }
    }
  }

  afterReleaseBuild.dependsOn publish
  publish.finalizedBy closeAndReleaseRepository
}
