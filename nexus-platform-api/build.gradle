plugins {
  id 'java-library'
  id 'com.github.johnrengelman.shadow'
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
  mergeServiceFiles()
  archiveClassifier.set('')

  exclude 'groovy/**'
  exclude 'groovyjarjarantlr/**'
  exclude 'groovyjarjarasm/**'
  exclude 'groovyjarjarcommonscli/**'

  def prefix = 'nexus.shadow.'
  relocate('com.', "${prefix}com.") {
    exclude 'com.sonatype.**'
  }
  relocate('org.', "${prefix}org.") {
    exclude 'org.sonatype.**'
    exclude 'org.apache.commons.lang3.**'
    exclude 'org.slf4j.**'
    exclude 'org.xml.**'
    exclude 'org.w3c.**'
  }
}

artifacts {
  archives shadowJar
}

dependencies {
  implementation "com.sonatype.nexus:nexus-platform-api:$nexusPlatformApiVersion"
  implementation "commons-io:commons-io:$commonsIoVersion"
}
